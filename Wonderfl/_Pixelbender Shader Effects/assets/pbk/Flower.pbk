<languageVersion : 1.0;>

kernel Flower
<   namespace : "Simo Endre";
    vendor : "simoendre.wordpress.com";
    version : 1;
>
{
    input image4 src;
    output pixel4 dst;
    
    parameter float offsetX
    <
        minValue : float(1.0);
        maxValue : float(10.0);
    >;
    
    parameter float offsetY
    <
        minValue : float(0.0);
        defaultValue : float(2.0);
        maxValue : float(20.0);
    >;
    
    parameter float2 resolution
    <
        minValue : float2(460.0, 460.0);
        defaultValue : float2(800.0, 600.0);
        maxValue : float2(1024.0, 768.0);
        description : "define the screen resolution";
    >;
    
    parameter float time
    <
        minValue : float(0.0);
        defaultValue : float(0.0);
        maxValue : float(1000.0);
        description : "adjust time";
    >;
    
    parameter float red
    <
        minValue : float(0.0);
        defaultValue : float(0.8);
        maxValue : float(1.0);
    >;
    
    parameter float green
    <
        minValue : float(0.0);
        defaultValue : float(0.5);
        maxValue : float(1.0);
    >;
    
    parameter float blue
    <
        minValue : float(0.0);
        defaultValue : float(0.1);
        maxValue : float(1.0);
    >;
    
    void evaluatePixel()
    {
        float pi = 3.1215927;
        float2 pos = -1.0 + 2.0 * outCoord().xy / resolution.y;
        float a = atan(pos.y, pos.x);
        float r = sqrt(pow(pos.x, 2.0) + pow(pos.y, 2.0)) * 0.8;
        float w = cos(pi*time - r*offsetY);
        float u = 0.5+0.5*cos(12.0*a-w*7.0+(r*3.0/0.9*offsetX));
        float v = 0.25+0.75*pow(u,1.0*r)*(0.7+0.3*w);
        
        float t = float(v-r) > 0.0 ? 0.6 : 0.0;
        
        float color = t * sqrt(1.0-r/v)*r*4.0;
        color *= 1.25+0.25*cos((12.0*a-w*7.0+r*4.0)/2.0);
        color *= 1.0-0.35*(0.5+0.5*sin(r*30.0))*(0.5+0.5*cos(12.0*a-w*7.0+r*8.0));
        float4 c = float4(color-v*red+r*0.4*(1.2-r),color-u*green+r*0.2+0.35*u*(1.2-r),color-u*r+blue*u*(1.2-r), 1.0);
        
        dst.argb = float4(c.a, c.r, c.g, c.b);
    }
}
